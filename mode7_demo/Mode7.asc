// Track script



struct _m7Vector {
  float x, y, z;
};

int kart_sprite;

// filter
bool filter;
int filter_t = 40;


protected void Mode7::_DrawTrackObjects(DrawingSurface* ds, float cam_y, int angle, int ox, int oy) {
  
  float x = -356.0;
  float z = -194.0;
  int sl = 19;
  
  // move track sprite according to camera position
  float sin = Maths.Sin(cam_y);
  float cos = Maths.Cos(cam_y);
  int xx = FloatToInt(x * cos - z * sin, eRoundNearest);
  int zz = FloatToInt(x * sin + z * cos, eRoundNearest);

  DynamicSprite* temp = DynamicSprite.CreateFromExistingSprite(sl);
  if (angle) temp.Rotate(angle);
  xx = ds.Width/2 +  xx - (temp.Width)/2;
  zz = ds.Height/2 + zz - (temp.Height)/2;
  ds.DrawImage(xx, zz, temp.Graphic);
  temp.Delete();
}

bool debug_use_dyn = false;

protected void Mode7::_GenerateTrackSprite() {
   
  // calculate track angle
  this._track_angle = FloatToInt(-this._camera_angle_y, eRoundNearest);
  while (this._track_angle < 0) this._track_angle += 360;
  while (this._track_angle > 359) this._track_angle -= 360;
  
  float cam_y = Maths.DegreesToRadians(IntToFloat(this._track_angle));
  
  this._track_sprite = DynamicSprite.CreateFromExistingSprite(this._track_sprite_slot);
  if (this._track_angle) this._track_sprite.Rotate(this._track_angle);

    
  // sprite offset due to rotation
  int ox = (Game.SpriteWidth[this._track_sprite_slot]  - this._track_sprite.Width)  / 2;
  int oy = (Game.SpriteHeight[this._track_sprite_slot] - this._track_sprite.Height) / 2;
  
  // draw trackobjects
  DrawingSurface* ds = this._track_sprite.GetDrawingSurface();
  this._DrawTrackObjects(ds, cam_y, this._track_angle, ox, oy);
  ds.Release();

  // move track sprite according to camera position
  float sin = Maths.Sin(cam_y);
  float cos = Maths.Cos(cam_y);
  float cox_f = this._camera_position_x * cos - this._camera_position_z * sin;
  float coz_f = this._camera_position_x * sin + this._camera_position_z * cos;
  int cox = FloatToInt(cox_f, eRoundNearest);
  int coz = FloatToInt(coz_f, eRoundNearest);
  
  // final position of rotated track sprite on track canvas
  int x = (this._track_canvas_size/2 - Game.SpriteWidth[this._track_sprite_slot]/2  + ox) - cox;
  int y = (this._track_canvas_size/2 - Game.SpriteHeight[this._track_sprite_slot]/2 + oy) - coz + this._track_canvas_y_offset;
  this._track_sprite.ChangeCanvasSize(this._track_canvas_size, this._track_canvas_size, x, y);
}

float point_angle, angle, z_t, dist, z_dist;
int t_x, t_z, t_w;  // texture coords

int precision = 1000000;

int green = 13823;


protected void Mode7::_DrawTrack3D() {  

  this._GenerateTrackSprite();

  DrawingSurface* ds = Room.GetDrawingSurfaceForBackground();
  ds.Clear(green);

  // "skybox"
  ds.DrawingColor = Game.GetColorFromRGB(255, 239, 148);
  // calculate horizon
  int hor_y = this._screen_y + this._screen_height/2 - FloatToInt(Maths.Tan(Maths.DegreesToRadians(this._camera_angle_x)) * this._camera_dist, eRoundNearest);
  if (hor_y >= 0) {
    ds.DrawRectangle(0, 0, this._screen_width - 1, hor_y - 1);
    int sbx = ((this._track_angle*256)/60) % 256 - 256;
    ds.DrawImage(sbx, hor_y - Game.SpriteHeight[this._horizon_sprite_slot], this._horizon_sprite_slot);
  }

  int y = this._screen_y;
  float y_screen = -IntToFloat(this._screen_height - 1) / 2.0;
  DynamicSprite* t;
  
  // screen width, used for distance calculation
  float s_w = IntToFloat(this._screen_width);
  
  // main loop
  while (y < this._screen_y + this._screen_height) {
    
    // angle between camera's horizon and current scanline
    point_angle = Maths.ArcTan2(y_screen, this._camera_dist);
    // angle between line camera-scanline & ground
    angle = Maths.DegreesToRadians(this._camera_angle_x) + point_angle;
    // z coordinate of respective ground line
    z_t = this._camera_position_y / Maths.Tan(angle);
    // distance between camera and ground line
    dist = this._camera_position_y / Maths.Sin(angle);
    // distance between camera's image plane and ground line, used to shrink scanline
    z_dist = dist * Maths.Cos(point_angle);
    
    // width and x of scan line
    float t_w_f = (s_w * z_dist) / this._camera_dist;
    t_w = FloatToInt(t_w_f, eRoundNearest);
    
    // too far away -> scan line to wide -> grab less, draw centered
    int f = 1;
    int draw_width = this._screen_width + 4; // saw tooth requires wider image
    if (t_w > this._track_canvas_size) {
      f = (precision * t_w) / this._track_canvas_size;
      t_w = this._track_canvas_size;
      t_w_f = IntToFloat(t_w);
      draw_width = (draw_width * precision) / f;
    }
    int draw_x = (this._screen_width - draw_width) / 2;
    
    t_x = (this._track_canvas_size - t_w) / 2;
    // saw tooth correction
    if (t_w % 2) draw_x -= 2;
    
    // calculate y coordinate of line on canvas
    t_z = this._track_canvas_size / 2 - FloatToInt(z_t, eRoundNearest) + this._track_canvas_y_offset;
    if (z_t > 0.0 && t_z >= 0 && t_z < this._track_canvas_size) {
      
      DrawingSurface* canvas = this._track_sprite.GetDrawingSurface();
      t = DynamicSprite.CreateFromDrawingSurface(canvas, t_x, t_z, t_w, 1);
      canvas.Release();
      if (filter) {
        ds.DrawImage(draw_x+1, y-1, t.Graphic, filter_t, draw_width-2, 1);
        ds.DrawImage(draw_x-1, y+1, t.Graphic, filter_t, draw_width+2, 1);
      }
      ds.DrawImage(draw_x, y, t.Graphic, 0, draw_width, 1);
    }
    
    y_screen += 1.0;
    y++;
    if (filter) {
      y_screen += 1.0;
      y++;
    }
  }  
  ds.Release();
}

protected void Mode7::_DrawObjects() {
  
  int fnd_objects = 0;
  
  float ox[] = new float[this._obj_count];
  float oy[] = new float[this._obj_count];
  float zd[] = new float[this._obj_count];
  int ob[] = new int[this._obj_count];
  
  // get object's position relative to camera and check distance
  int i;
  _m7Vector o;
  while (i < this._obj_count) {
    o.x = this._obj_x[i];
    o.z = this._obj_z[i];
   
    // translate object's x,z to camera's coords
    float cox = o.x - this._camera_position_x;
    float coz = -(o.z - this._camera_position_z);
    
    // rotate by negative camera's y angle
    float sin = Maths.Sin(Maths.DegreesToRadians(this._camera_angle_y));
    float cos = Maths.Cos(Maths.DegreesToRadians(this._camera_angle_y));
    o.x = cox * cos - coz * sin;
    o.z = cox * sin + coz * cos;
    
    // object is in front of camera
    if (o.z > 0.0) {
      
      // angle between line from camera to object and ground
      float obj_angle = Maths.ArcTan2(this._camera_position_y - this._obj_y[i], o.z);
      dist = o.z / Maths.Cos(obj_angle);
      // translate x angle to camera
      obj_angle -= Maths.DegreesToRadians(this._camera_angle_x);
      o.y = dist * Maths.Sin(obj_angle);
      z_dist = dist * Maths.Cos(obj_angle);
      
      if (z_dist > 0.1) {
        
        // add object to list
        ox[fnd_objects] = o.x;
        oy[fnd_objects] = o.y;
        zd[fnd_objects] = z_dist;
        ob[fnd_objects] = i;
        fnd_objects++;
      }
    }
    i++;
  }
  
  // no object visible?
  if (fnd_objects == 0) return;

  // init z-buffer    
  i = 0;
  while (i < fnd_objects) {
    this._obj_order[i] = i;
    i++;
  }

  // sort found objects
  if (fnd_objects > 1) {
    
    // bubble sort
    int j;
    i = 0;
    while (i < fnd_objects - 1) {
        j = i;
        while (j < fnd_objects) {
            if (zd[this._obj_order[i]] < zd[this._obj_order[j]]) {
              int swap = this._obj_order[i];
              this._obj_order[i] = this._obj_order[j];
              this._obj_order[j] = swap;
            }
          j++;
        }
      i++;
    }
  }

  // finally, draw sorted objects
  DrawingSurface* ds = Room.GetDrawingSurfaceForBackground();

  int in, slot, w, h, obin; // in: fnd_list index, obin: all objects list index
  int x2d, y2d;
  i = 0;
  while (i < fnd_objects) {
    in = this._obj_order[i];
    obin = ob[in];
    
    // translate x and y according to z_dist      
    if (zd[in] > 0.1) {
      float f = this._camera_dist / zd[in];
      
      slot = this._obj_graphic[obin];
      
      // kart slot
      bool flipped = false;
      if (obin == 0) 
      {
        if (kart_sprite <= 0) {
          slot = 27 - kart_sprite;
        } else {
          slot = 43 - kart_sprite;
        }
        
        //if (kart_sprite < 0) {
        //  slot = 8 - kart_sprite;
        //  flipped = true;
        //}
        //else slot = 8 + kart_sprite;
        
      }

      // turn into 2D coords
      x2d = this._screen_x + this._screen_width/2  + FloatToInt(ox[in] * f, eRoundNearest);
      y2d = this._screen_y + this._screen_height/2 + FloatToInt(oy[in] * f, eRoundNearest);
      // size
      w = FloatToInt(IntToFloat(Game.SpriteWidth[slot])  * f * this._obj_factor[obin], eRoundNearest);
      h = FloatToInt(IntToFloat(Game.SpriteHeight[slot]) * f * this._obj_factor[obin], eRoundNearest);
     
      int obj_d_x = x2d - w/2;
      int obj_d_y = y2d - (h*9/10);
      

      //if(this._obj_prev_graphic[obin] != slot) {
      
        if (flipped) {
          DynamicSprite* tt = DynamicSprite.CreateFromExistingSprite(slot);
          tt.Flip(eFlipLeftToRight);
          
        //  this._obj_ovr[obin] = Overlay.CreateGraphical(obj_d_x, obj_d_y, tt.Graphic, true);
          ds.DrawImage(obj_d_x, obj_d_y, tt.Graphic, 0, w, h);
          tt.Delete();
        }
        else {
          ds.DrawImage(obj_d_x, obj_d_y, slot, 0, w, h);
          //this._obj_ovr[obin] = Overlay.CreateGraphical(obj_d_x, obj_d_y, slot, true);
        }
      //} 
      
      //if(this._obj_ovr[obin] != null) {
      //  this._obj_ovr[obin].X = obj_d_x;
      //  this._obj_ovr[obin].Y = obj_d_y;
      //  this._obj_ovr[obin].ZOrder = i;
      //}
      
      this._obj_prev_graphic[obin] = slot;
    }
    
    i++;
  }
  ds.Release();

}

protected void Mode7::_CameraTrack(eCameraTargetType camType, float target_x, float target_y, float target_z,  float teta_angle) {

  _m7Vector target;
  target.x = target_x;
  target.y = target_y;
  target.z = target_z;  
  
  float camera_angle_target = teta_angle;
  
  // determine kart sprite
  int apart = FloatToInt(camera_angle_target - this._camera_angle_y, eRoundNearest);
  while (apart > 180) apart -= 360;
  while (apart < -180) apart += 360;
  kart_sprite = apart / 22;

  if (camType == eCameraTarget_Behind)
  {
    // set camera behind kart
    float behind = 50.0;
   
    // make it lazy
    this._camera_angle_y += (camera_angle_target - this._camera_angle_y) * 0.05;
    
    float sin = Maths.Sin(Maths.DegreesToRadians(this._camera_angle_y));
    float cos = Maths.Cos(Maths.DegreesToRadians(this._camera_angle_y));
    this._camera_position_x = target.x - behind * sin;
    this._camera_position_z = target.z + behind * cos;
  }
  else if (camType == eCameraTarget_Sides) 
  {
    this._camera_position_x = 100.0;
    this._camera_position_z = 100.0;
    float dx = this._camera_position_x-target.x;
    float dz = this._camera_position_z-target.z;
    this._camera_angle_y = -Maths.RadiansToDegrees(Maths.ArcTan2(dx, dz));
    float target_dist = Maths.Sqrt(dx * dx + dz * dz);
    this._camera_angle_x = Maths.RadiansToDegrees(Maths.ArcTan2(this._camera_position_y, target_dist));
    target_dist = target_dist * 3.0;
    if (target_dist < 100.0) target_dist = 100.0;
    this._camera_dist = target_dist;
    //lblDebug.Text = String.Format("%f", this._camera_angle_x);
  }
}  

void Mode7::SetObj(int slot, float x, float y, float z)
{
  this._obj_x[slot] = x;
  this._obj_y[slot] = y;
  this._obj_z[slot] = z;
}

void Mode7::TargetCamera(float target_x, float target_y, float target_z,  float teta_angle, eCameraTargetType camType) 
{  
  this._CameraTrack(camType, target_x, target_y, target_z, teta_angle);
}

void Mode7::Draw() 
{  
  this._DrawTrack3D();
  
  this._DrawObjects();
}


void Mode7::SetCamera(float x, float y, float z, float xa, float ya, float focal_length)
{
  this._camera_position_x = x;
  this._camera_position_y = y;
  this._camera_position_z = z;
  this._camera_angle_x = xa;
  this._camera_angle_y = ya;
  this._camera_dist = focal_length;
}

void Mode7::AddObject(int x, int z, float factor, int slot)
{
  if (this._obj_count == MAX_OBJECTS) return;
  this._obj_x[this._obj_count] = IntToFloat(x);
  this._obj_y[this._obj_count] = 0.0;
  this._obj_z[this._obj_count] = IntToFloat(z);
  this._obj_factor[this._obj_count] = factor;
  this._obj_graphic[this._obj_count] = slot;
  this._obj_count++;
}

void Mode7::SetViewscreen(int x, int y, int width, int height) 
{
  this._screen_x = x;
  this._screen_y = y;
  this._screen_width = width;
  this._screen_height = height;
}

void Mode7::SetGroundSprite(int ground_graphic)
{
  this._track_canvas_size = 1000;
  this._track_canvas_y_offset = 500;  // camera position on track_canvas below center
  
  this._track_sprite_slot = ground_graphic;  
}

void Mode7::SetHorizonSprite(int horizon_graphic)
{
  this._horizon_sprite_slot = horizon_graphic; 
}

void Mode7::DebugKeyPress(eKeyCode k)
{
  if (k == eKeyUpArrow   && this._camera_angle_x > -40.0) this._camera_angle_x -= 1.0;
  if (k == eKeyDownArrow && this._camera_angle_x <  55.0) this._camera_angle_x += 1.0;
  if (k == eKeyPlus   && this._camera_dist < 400.0) this._camera_dist += 1.0;
  if (k == eKeyHyphen && this._camera_dist >  10.0) this._camera_dist -= 1.0;
  if (k == eKeyI) this._camera_position_y += 1.0;
  if (k == eKeyK) this._camera_position_y -= 1.0;
  if (k == eKeyO) this._camera_position_x += 1.0;
  if (k == eKeyL) this._camera_position_x -= 1.0;

  // debug
  if (k == eKeyLeftArrow) this._camera_angle_y += 1.0;
  if (k == eKeyRightArrow) this._camera_angle_y -= 1.0;  
}
